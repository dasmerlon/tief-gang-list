run:
    npx parcel --open

build:
    rm -rf dist
    just generate-api-client
    npx parcel build

setup:
    npm install

lint *args:
    npx eslint {{ args }} .
    npx tsc

# This job takes the OpenApi specification generated by the FastAPI backend
# and runs OpenApi-Generator on it.
# This then outputs fully functional typescript code, that's put into `src/api`.
# Those functions can then just be called from inside the code without having to
# write any schemas or functions.
generate-api-client *args:
    #!/bin/bash
    cd ../backend
    just generate-api-spec
    cd ../frontend
    rm -rf src/api
    npx openapi-generator-cli generate \
        --input-spec ../backend/openapi.json \
        --generator-name typescript-fetch \
        --output ./src/api \
        --additional-properties \
    "supportsES6=true,\
    withInterfaces=true,\
    enumNameSuffix=,\
    enumPropertyNaming=UPPERCASE,\
    modelPropertyNaming=camelCase,\
    typescriptThreePlus=true,\
    stringEnums=true\
    " {{ args }}

format:
    just lint --fix
